//import static org.springframework.boot.gradle.SpringBootPluginExtension.LayoutType.*

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.0"
        classpath "org.grails.plugins:hibernate5:7.0.5"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.2.4"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "2.3.2" // TODO
}

version "3.0.0-SNAPSHOT"
group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin:"org.grails.grails-gsp"
apply plugin:"maven-publish"

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.grails.org/grails/core" }
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

//grails {
//    plugins {
//        compile project(':openapi-plugin')
//    }
//}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:cache-ehcache:3.0.0"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-jcache"
    runtime 'org.ehcache:ehcache'

    compile "org.grails.plugins:gsp"
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "org.glassfish.web:el-impl:2.1.2-b03"
    runtime "javax.xml.bind:jaxb-api:2.3.1"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:3.2.4"
    testCompile "io.micronaut:micronaut-inject-groovy"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.mockito:mockito-core"
    testCompile "org.grails:grails-web-testing-support"
    testCompile "org.grails.plugins:geb"
    testCompile "org.seleniumhq.selenium:selenium-remote-driver:3.14.0"
    testCompile "org.seleniumhq.selenium:selenium-api:3.14.0"
    testCompile "org.seleniumhq.selenium:selenium-support:3.14.0"
    testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:3.14.0"
    testRuntime "org.seleniumhq.selenium:selenium-firefox-driver:3.14.0"

    // Groovy backwards compat
    runtime 'org.codehaus.groovy:groovy-dateutil'

    // Grails plugin dependencies

    runtime "org.grails.plugins:ala-bootstrap3:4.0.0"
    compile "org.grails.plugins:ala-ws-plugin:3.0.0"
    compile "org.grails.plugins:ala-ws-security-plugin:4.1.0-SNAPSHOT"
    compile "org.grails.plugins:ala-auth:5.1.0-SNAPSHOT"
    compile "org.grails.plugins:ala-admin-plugin:2.1"

    compile 'dk.glasius:external-config:2.0.1'
    compile 'org.grails.plugins:http-builder-helper:1.1.0'
    compile "org.grails.plugins:csv:1.0.1"
    compile 'org.grails.plugins:mail:3.0.0'

    compile "org.grails.plugins:oauth:4.0.0"
    // transitive oauth plugin deps used in code
    compile "com.github.scribejava:scribejava-core:4.0.0"
    compile "com.github.scribejava:scribejava-apis:4.0.0"

    compile "domurtag.plugins:grails-simple-captcha:1.0.0-grails3"
    compile "org.grails.plugins:export:2.0.0"
    compile 'org.grails.plugins:grails-markdown:3.0.0'

    // regular JAR dependencies

    runtime 'com.zaxxer:HikariCP:3.3.1'
    runtime 'mysql:mysql-connector-java:8.0.25'
    testRuntime "com.h2database:h2"
    compile 'com.google.guava:guava:21.0'
    compile 'org.apache.httpcomponents:httpcore:4.3.3'
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
    compile 'org.apache.httpcomponents:httpmime:4.3.3'
    compile 'org.apache.commons:commons-lang3:3.12.0'
    compile 'commons-io:commons-io:2.6'
    compile 'commons-beanutils:commons-beanutils:1.8.3'
    compile 'org.mindrot:jbcrypt:0.4'
    compile 'com.opencsv:opencsv:5.5.2'

    compile 'org.springframework.session:spring-session-data-mongodb'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
    compile 'org.grails.plugins:spring-session-helper:2.0.0'

    compile "com.auth0:java-jwt:3.18.2"
    compile "com.auth0:jwks-rsa:0.20.0"

    // Spring Security to secure actuator endpoints
    // TODO Upgrade or decommission boot admin
//    compile 'de.codecentric:spring-boot-admin-starter-client:2.6.2'
//    compile 'org.springframework.boot:spring-boot-starter-security'
//    compile 'org.springframework.security:spring-security-web'

    compile('org.webjars.bower:chosen:1.8.3')
    compile('org.webjars.bower:chosen-bootstrap:1.1.0') {
        exclude module: 'jquery' // dependency from skin
        exclude module: 'bootstrap' // dependency from skin
    }

    compile('au.org.ala.plugins:openapi:1.0.0')

    testCompile('com.squareup.retrofit2:retrofit-mock:2.4.0')
}

springBoot {
    buildInfo()
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

bootWar {
    launchScript()
    classifier = 'exec'
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}

assets {
    minifyJs = true
    minifyCss = true
    includes = ["fonts/*"]
}

publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            artifact bootWar
        }
    }
}
